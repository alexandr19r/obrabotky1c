
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	//Вставить содержимое обработчика.
	
	ДлительнаяОперация = СформироватьРеализацииНаСервере(ПараметрКоманды);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриЗавершенииСозданияДокументовРеализаций", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ПараметрыВыполненияКоманды.Источник);
	ПараметрыОжидания.Вставить("ТекстСообщения", НСтр("ru = 'Создание документов реализаций...'"));
	ПараметрыОжидания.Вставить("ВыводитьПрогрессВыполнения", Истина);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
	
	//Для отладки
	//СтруктураПараметров = Новый Структура;
	//СтруктураПараметров.Вставить("МассивДокументов", ПараметрКоманды);
	//АдресВХранилище = Неопределено;
	//ЭР_РеализацияТоваровУслугДополнение.СоздатьРеализацииПоЗаказамИСкладам(ПараметрКоманды, АдресВХранилище);

КонецПроцедуры

&НаСервере
Функция СформироватьРеализацииНаСервере(МассивДокументов)
	
	НаименованиеЗадания = НСтр("ru = 'Формирование документов реализаций'");
	ВыполняемыйМетод = "ЭР_РеализацияТоваровУслугДополнение.СоздатьРеализацииПоЗаказамИСкладам"; 
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("МассивДокументов", МассивДокументов);
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(ВыполняемыйМетод, СтруктураПараметров, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура ПриЗавершенииСозданияДокументовРеализаций(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.КраткоеПредставлениеОшибки);
		Возврат;
	ИначеЕсли Результат.Статус = "Выполнено" Тогда
		//ПриЗавершенииСозданияПлатежныхДокументовНаСервере();
		РезультатВыполнения = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		
		СозданныеДокументы = РезультатВыполнения.СозданныеДокументы;
		Если Не СозданныеДокументы.Количество() = 0 Тогда
			ТекстСообщения = "";
			Для Каждого Документ Из СозданныеДокументы Цикл
				ТекстСообщения = ТекстСообщения + Строка(Документ) + Символы.ПС;
			КонецЦикла;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		
		Если РезультатВыполнения.ЕстьОшибки Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(РезультатВыполнения.ОшибкиВыполнения);
		КонецЕсли;	
		
	КонецЕсли;
	
КонецПроцедуры

//Сервер общий модуль
Процедура СоздатьРеализацииПоЗаказамИСкладам(ПараметрыВыполнения, АдресРезультата) Экспорт
КонецПроцедуры
